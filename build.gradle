plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.ow2.asm:asm:9.6")
    implementation("org.ow2.asm:asm-tree:9.6")
    implementation("com.eclipsesource.minimal-json:minimal-json:0.9.5")

    implementation("org.ow2.asm:asm-util:9.6")

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.10.0")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.10.0")
}

test {
    def wd = project.projectDir

    jvmArgs += [
        *(project.hasProperty('debug') ? ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=y"] : []),
        "-XX:+AllowEnhancedClassRedefinition",
        "-Xshare:off",
        "-javaagent:${wd}\\build\\libs\\mixin.jar",
        "-Dmixin.files=${wd}\\src\\test\\resources\\mixin.json"
    ]

    classpath = classpath - sourceSets.main.output

    useJUnitPlatform()
}

jar {
    manifest {
        from('src/main/resources/META-INF/MANIFEST.MF')
    }

    exclude 'module-info.class'
    duplicatesStrategy = DuplicatesStrategy.FAIL
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
