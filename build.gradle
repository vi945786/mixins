plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.ow2.asm:asm:9.6'
    implementation 'org.ow2.asm:asm-tree:9.6'
    implementation 'org.ow2.asm:asm-util:9.6'

    implementation 'com.eclipsesource.minimal-json:minimal-json:0.9.5'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.platform:junit-platform-launcher'
}

test {
    def wd = project.projectDir

    jvmArgs += [
        *(project.hasProperty('debug') ? ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=y"] : []),
        "-XX:+AllowEnhancedClassRedefinition",
        "-Xshare:off",
        "-javaagent:${wd}\\build\\libs\\mixin.jar=${wd}\\src\\test\\resources\\mixin.json"
    ]

    classpath = classpath - sourceSets.main.output

    useJUnitPlatform()
}

jar {
    archiveBaseName = "mixin"

    manifest {
        from('src/main/resources/META-INF/MANIFEST.MF')
    }

    exclude 'module-info.class'
    duplicatesStrategy = DuplicatesStrategy.FAIL
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
